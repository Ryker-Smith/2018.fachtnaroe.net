@echo off
:: AnnoyTheCat.bat
:: Plays every sound you have.
:: Because it uses %0, it must be run via the full
:: filename or by double-clicking it in Explorer.

:: If there is no argument, do GETFILES
:: otherwise do PLAY
if "%1"=="" goto GETFILES
goto PLAY

:GETFILES
echo   Press Ctrl-C to stop the play sequence.
:: Turn long file name expansion for FOR off.
:: Short file names make things much easier.
lfnfor off
:: Use FOR to get all the filenames. Then CALL this
:: batch file, passing the filename as an argument.
for %%x in (c:\windows\media\*.wav) do call %0 %%x
:: These next 3 lines insure this DOS window closes.
@echo off
cls
exit
goto DONE

:PLAY
:: Use CHOICE to effect a time delay between sounds.
choice /t:y,10 > nul
:: Here the sound gets played. START uses /W to "wait"
:: until the sound is done and /M to run the SNDREC32
:: program "minimized".
start /w /m sndrec32.exe /play /close %1

:DONE
::  http://www.calweb.com/~webspace
